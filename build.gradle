buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.3.RELEASE")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = "io.talken"
    version = "1.0.0"

    bootJar {
        baseName = "talken-" + project.name
    }

    jar {
        baseName = "talken-" + project.name
        manifest.attributes provider: 'gradle'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "https://dl.bintray.com/ethereum/maven/" }
//        mavenLocal()
        maven {
            credentials {
                username talkenRepositoryUser
                password talkenRepositoryPass
            }
            url talkenRepositoryUrl
        }
    }

    configurations {
        compile.exclude module: "spring-boot-starter-tomcat"
        all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        }
    }

    dependencies {
        compile('io.talken:java-common:1.0.0') { changing = true }

        compileOnly('org.projectlombok:lombok:1.18.4')

        // Spring cache
        compile('org.springframework.boot:spring-boot-starter-cache')

        // Spring Data
        compile('org.springframework.boot:spring-boot-starter-jdbc')
        compile('org.springframework.boot:spring-boot-starter-aop')

        // configuration processor
        compile('org.springframework.boot:spring-boot-configuration-processor')

        // google http client
        compile('com.google.http-client:google-http-client-jackson2:1.27.0')

        // Redis
        compile('org.springframework.boot:spring-boot-starter-data-redis')
        compile('redis.clients:jedis')

        // stellar sdk
        compile('com.github.stellar:java-stellar-sdk:0.6.0')

        // ethereum web3j sdk
        compile('org.web3j:core:4.2.0')

        // devtools
        runtime('org.springframework.boot:spring-boot-devtools')

        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile('junit:junit:4.12')
    }
}

project(':dex-api') {
    dependencies {
        compile project(":dex-shared")
    }
}

project(':dex-gov') {
    dependencies {
        compile project(":dex-shared")
    }
}